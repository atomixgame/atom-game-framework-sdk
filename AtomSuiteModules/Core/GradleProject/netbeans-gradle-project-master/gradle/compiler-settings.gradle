ext.configureJavaCompilers = {int javaVersion ->
    def requiredVersion = JavaVersion.toVersion("1.${javaVersion}");

    sourceCompatibility = requiredVersion
    targetCompatibility = requiredVersion

    def compileTasks = tasks.withType(JavaCompile);
    def compilerOptions = compileTasks*.options;
    compilerOptions*.encoding = 'UTF-8';
    compilerOptions*.compilerArgs = ['-Xlint'];

    if (JavaVersion.current() != requiredVersion) {
        String jdkProperty = "jdk${javaVersion}Compiler";
        if (project.hasProperty(jdkProperty)) {
            compilerOptions*.fork = true;
            compilerOptions*.forkOptions*.executable = project.property(jdkProperty).toString().trim();
        }
        else {
            compileTasks*.doFirst {
                logger.warn "Warning: ${jdkProperty} property is missing and not compiling with Java ${requiredVersion}. Using ${JavaVersion.current()}";
            }
        }
    }

}